plugins {
    id 'java'
    id 'application'
    id 'distribution'
    id 'com.github.jk1.dependency-license-report' version '2.0'
}

group 'org.jlab'
version '2.3.0'

ext.confluentVer = '7.0.1'
ext.kafkaVer = '3.0.0'

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()

    maven {
        url "https://packages.confluent.io/maven"
    }
}

dependencies {
    implementation "org.apache.kafka:kafka-streams:" + kafkaVer,
                "io.confluent:kafka-streams-avro-serde:" + confluentVer,
                "org.apache.avro:avro:1.10.2",
                "org.slf4j:slf4j-log4j12:1.7.30",
                "org.jlab:jaws-libj:1.9.0"
    testImplementation "org.apache.kafka:kafka-streams-test-utils:" + kafkaVer,
                "junit:junit:4.13.1"
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

jar {
    archiveFileName = 'registrations2epics.jar'
}

application {
    mainClass.set('org.jlab.jaws.Registrations2Epics')
}

build {
    dependsOn(installDist)
}

installDist.dependsOn(generateLicenseReport)
distTar.dependsOn(generateLicenseReport)
distZip.dependsOn(generateLicenseReport)

run {
    classpath += files('config')
}

distributions {
    main {
        contents {
            from("build/reports/dependency-license") {
                into "dependency-license"
            }
            from('config') {
                into 'config'
            }
        }
    }
}

startScripts {
    classpath += files('config')

    // https://discuss.gradle.org/t/classpath-in-application-plugin-is-building-always-relative-to-app-home-lib-directory/2012
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\config', '%APP_HOME%\\config')
        unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/config', '$APP_HOME/config')
    }
}